import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.nio.file.Path
import java.util.Optional

plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
    maven {
        url = uri('https://code.mschae23.de/api/packages/mschae23/maven')
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from('COPYING')
    from('COPYING.LESSER')
}

// Configure the Maven publication
publishing {
    publications {
        mod(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "Forgejo"
            url = uri("https://code.mschae23.de/api/packages/mschae23/maven")

            credentials(HttpHeaderCredentials) {
                final token = Optional.ofNullable(System.getenv("XDG_CONFIG_HOME")).map { Path.of(it).resolve("packages") }.or {
                    Optional.ofNullable(System.getenv("HOME")).map { Path.of(it).resolve(".config/packages") }}.or {
                    Optional.ofNullable(System.getenv("HOMEDRIVE")).flatMap { drive -> Optional.ofNullable(System.getenv("HOMEPATH"))
                        .map { drive + it }}.map { Path.of(it).resolve(".packages") }}.map {
                    it.resolve("mschae23-forgejo.token") }.filter { Files.exists(it) }.map {
                    Files.readString(it, StandardCharsets.UTF_8)
                }.orElse("")

                name = "Authorization"
                value = "token $token"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }

        maven {
            name = "GitHubPackages"
            url = uri('https://maven.pkg.github.com/mschae23/codec-config-api')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('GITHUB_USERNAME')
                password = project.findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}
