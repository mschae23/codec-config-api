plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url = uri('https://pkg.mschae23.de/maven')
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "org.relativitymc:modern-yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

loom {
    useIntermediateMappings = true
    intermediaryUrl = 'https://repo.codemc.io/repository/relativitymc/org/relativitymc/intermediary/%1$s/intermediary-%1$s-v2.jar'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from('COPYING')
    from('COPYING.LESSER')
}

// Configure the Maven publication
publishing {
    publications {
        mod(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "Forgejo"
            url = uri("https://pkg.mschae23.de/maven")

            credentials(HttpHeaderCredentials) {
                final token = java.util.Optional.ofNullable(System.getenv("XDG_CONFIG_HOME")).map { java.nio.file.Path.of(it).resolve("packages") }.or {
                    java.util.Optional.ofNullable(System.getenv("HOME")).map { java.nio.file.Path.of(it).resolve(".config/packages") }}.or {
                    java.util.Optional.ofNullable(System.getenv("HOMEDRIVE")).flatMap { drive -> java.util.Optional.ofNullable(System.getenv("HOMEPATH"))
                        .map { drive + it }}.map { java.nio.file.Path.of(it).resolve(".packages") }}.map {
                    it.resolve("mschae23-forgejo.token") }.filter { java.nio.file.Files.exists(it) }.map {
                    java.nio.file.Files.readString(it, java.nio.charset.StandardCharsets.UTF_8)
                }.orElse("")

                name = "Authorization"
                value = "token $token"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
